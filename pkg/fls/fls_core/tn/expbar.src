use uo;
use os;
use cfgfile;

include "include/say";
include "include/client";
include ":gumps:gumps_ex";
include ":attributes:attributes";

program expBar(params)
   var who := params[1];
   var no_start := params[2];
   if (no_start)
      CloseGump(who, GUMPID_EXPBAR, 2);
      return;
   endif
   SetObjProperty(who, "#expbar", GetPid());
   var x := 200;
   var y := 500;
	var expbar := GFCreateGump();
   GFPage(expbar, 0);
   GFClosable(expbar, 0);
   GFSetId(expbar, GUMPID_EXPBAR);
   GFResizePic(expbar, x+7, y+8, 9270, 526, 66);
   GFResizePic(expbar, x+21, y+21, 9300, 502, 39);
   var color := "#332a1c";
   if (who.getExp() >= GetExpCap(who))
      color := "#FF6666";
   elseif (cint(who.account.GetProp("expboost")) >= who.getLevel())
      color := "#ADD8E6";
   endif

   GFHTMLArea(expbar, x+30, y+23, 173, 20, "<BASEFONT Size=5 color={}>Experiência".format(color));
   GFToolTipText(expbar, "Preto: Normal<br>Vermelho: Limite atingido<br>Azul: Boost de EXP");
   GFGumpPic(expbar, x+499, y+31, 9771, 0);
   GFGumpPicTiled(expbar, x+30, y+48, 480, 6, 0x2616);

   var reopen := 1;
   while (who.connected)
      GFCloseGumpTimed(who, GUMPID_EXPBAR, 60);
      var temp_gump := expbar;
      var exp_width := getExpLength(who.getExp(), who.getMaxExp(), 480);
      GFGumpPicTiled(temp_gump, x+30, y+48, exp_width, 6, 9751);

      var i := GFSendGump(who, temp_gump);
      if (i == 2)
         reopen := 0;
         return;
      endif

      if (RandomInt(5) == 1)
         break;
      endif
   endwhile

   if (reopen)
      start_script(":fls_core:tn/expbar", array{who});
   endif
endprogram

function getExpLength(exp, maxexp, maxwidth)
   return cint((exp * maxwidth) / maxexp);
endfunction