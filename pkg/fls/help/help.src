
use uo;
use datafile;
use cfgfile;

include ":tn:tngumps";
include ":gumps:gumps";
include ":gumps:yesno";
include ":gumps:gumps_ex";
include ":gumps:requestgump";
include "include/say";

const BUTTON_HELP_CANCEL := 1;
const BUTTON_HELP_SENDGMPAGE := 2;
const BUTTON_HELP_SENDCOUNPAGE := 3;
const BUTTON_HELP_REPORTBUG := 4;
const BUTTON_HELP_REPORTUSER := 5;
const BUTTON_HELP_HOTBAR := 6;

program help_menu(who)
	var chargump := GFCreateGump();
	var input;
	var first_name, last_name;
	first_name := "";
	last_name := "";

	GFDisposable(chargump, 0);
	GFResizePic(chargump, 50, 25, 5054, 350, 480);	
	GFGumpPic(chargump, 0,10, 10400);
	GFGumpPic(chargump, 368,10, 10410);
	
	GFGumpPic(chargump, 0,190, 10401);
	GFGumpPic(chargump, 368,190, 10411);
	
	GFGumpPic(chargump, 0,400, 10402);
	GFGumpPic(chargump, 368,400, 10412);
	
	GFTextMid(chargump, 70, 40, 330, 1153, "Menu de Ajuda");
	GFTextLine(chargump, 70, 75, 2103, "Lista de Comandos");

	var cfgtext := ReadConfigFile(":help:config/commands");
	var textelem := FindConfigElem( cfgtext, "commands" );
	var lines := GetConfigStringArray(textelem, "Line");
	GFHTMLArea(chargump, 70, 100, 310, 235, "<BR><BR>".join(lines), 9390, 1);

	GFTextLine(chargump, 125, 390-40, 1153, "Minha Barra de Habilidades Sumiu");
	GFTextLine(chargump, 125, 425-40, 1153, "Solicitar ajuda aos GMs");
	GFTextLine(chargump, 125, 460-40, 1153, "Reportar Bug ou fazer sugestão");
	GFTextLine(chargump, 125, 495-40, 1153, "Denunciar Jogador");
	GFAddButton(chargump, 70, 375-40, 4502, 4502, 1, BUTTON_HELP_HOTBAR );
	GFAddButton(chargump, 70, 410-40, 4502, 4502, 1, BUTTON_HELP_SENDGMPAGE );
	GFAddButton(chargump, 70, 444-40, 4502, 4502, 1, BUTTON_HELP_REPORTBUG );
	GFAddButton(chargump, 70, 479-40, 4502, 4502, 1, BUTTON_HELP_REPORTUSER );

	input := GFSendGump(who, chargump);
	input := input[0];

	if (input == BUTTON_HELP_SENDGMPAGE )
		var sendpage := YesNo(who, "Use a page somente caso seu problema possa ser resolvido por Staffers ou deseja alguma espécie auxílio. Continuar?", "Sim, quero enviar a msg.", "Não, não quero enviar a msg."  );
		if (sendpage)
			start_script( ":email:emailMessage/helpLetter", array{ who, 999999999} );
		endif

	elseif (input == BUTTON_HELP_REPORTBUG )
		var sendpage := YesNo(who, "Um bug é um defeito no jogo que será analisado pelos programadores e corrigido. Detalhe o máximo possivel como você descobriu o bug. Continuar?", "Sim, quero reportar o bug.", "Não, não tenho bug para reportar."  );
		if (sendpage)
			start_script( ":email:emailMessage/helpLetter", array{ who, 999999998 } );
		endif

	elseif (input == BUTTON_HELP_REPORTUSER )
		var sendpage := YesNo(who, "Use essa opção somente se desejar reportar um jogador por má conduta ou que esteja infringindo regras. Deseja continuar?", "Sim, quero reportar um jogdaor.", "Não."  );
		if (sendpage)
         SendSysMessageEx(who, "Que jogador deseja denunciar?", SSM_REQUEST);
         var targ := Target(who, TGTOPT_NOCHECK_LOS);
         if (!targ.isA(POLCLASS_MOBILE) || !targ.acctname)
            return SendSysMessageEx(who, "Só pode reportar jogadores.", SSM_FAIL);
         endif
			start_script( ":email:emailMessage/helpLetter", array{ who, 999999998, targ, "Reportar Jogador"} );
		endif
   elseif (input == BUTTON_HELP_HOTBAR)
      if (YesNo(who, "Deseja reposicionar o hotbar?", "Sim, reposicionar", "Manter na posição anterior"))
         var hotbar := GetObjProperty(who, "hotbar");
         hotbar.pos_x := 105;
         hotbar.pos_y := 543;
         SetObjProperty(who, "hotbar", hotbar);
      endif
      start_script(":charactercreation:hotbar/hotbar", who);
      start_script(":charactercreation:hotbar/controller", who);
	endif
endprogram
