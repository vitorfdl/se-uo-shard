use uo;
use math;
use polsys;
include "include/say";

CONST BYTE_DEFENDER := 1;

program requestAttack()
	Print( "INSTALLING: Double Click PH..." );
	return 1;
endprogram


exported function HandleDoubleClick( who, byref packet )

	var defender := SystemFindObjectBySerial(packet.GetInt32(BYTE_DEFENDER));


	//nao sei como, mas as vezes vem sem, entao deixa o pol tratar
	if (!defender)
		return 0;
	endif

	if (defender == who.backpack)
		return 0;
	endif

	if (defender.container == who.backpack)
		return 0;
	endif

	var equips := ListEquippedItems(who);
	if (defender in equips)
		return 0;
	endif

	//se tiver dentro de container, checa a distancia pro container raiz
	while (defender.container )
		defender := defender.container;
	endwhile

	if( Distance(who, defender) > 15 )
		SendSysMessageEx(who, "Voce esta muito distante.", SSM_FAIL);
		return 1;
	endif
	if ( abs(who.z - defender.z ) > 10 && defender.isa(POLCLASS_MOBILE))
		SendSysMessageEx(who, "Voce esta muito distance.", SSM_FAIL);
		return 1;
	endif
	if(!CheckLineOfSight(who, defender))
		SendSysMessageEx(who, "Voce não enxerga o alvo.", SSM_FAIL);
		return 1;
	endif

	return 0;
endfunction
