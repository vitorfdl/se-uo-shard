use uo;
use os;
use util;
use cfgfile;

include ":attributes:attributes";
include ":timedScripts:poisonUtil";
include ":tn:cooldown";
include "include/say";
include "include/damage";

program eat_food(who, food)
	EraseObjProperty(who, "IsMeditating");
	EraseObjProperty(who, "HealTimer");

	if(!Accessible(who, food))
		return;
	endif
	if(food.movable == 0)
		SendSysMessageEx(who, "Você não pode usar isto.", SSM_FAIL);
		return;
	endif

   var hungerlvl := AP_GetVital(who, "Hunger");
	if(hungerlvl <= 10)
		SendSysMessageEx(who, "Você esta muito cheio para comer mais.", SSM_FAIL);
		return;
	endif

	var foodcfg := ReadConfigFile(":*:itemdesc");
	var themsg;
	var foodvalue := Cint(foodcfg[food.objtype].foodvalue);

	var elem := foodcfg[food.objtype];
	foreach line in getconfigstringarray(elem, "leftovers")
		line := splitwords(line);
		var item := line[1];
		var amount := cint(line[2]);
		if (!createiteminbackpack(who, item, amount))
			for (amount; amount > 0; amount := amount-1) // For non-stackable items
				createiteminbackpack(who, item, 1);
			endfor
		endif
	endforeach

	if ( CINT(getObjProperty(food, "blessedfood")) == 1 ) 
		foodvalue := 5;
	endif

	var surprisemsg := "";
	var surpriseabove := "";
	if (food.objtype == 0xc92e) //silverleaf pie
		surprisemsg := "Você se sente corajoso!";
	elseif (food.objtype == 0x0c6d)//cebola
		if (GetCooldown(who, "cebola") > 0)
			surpriseabove := "*mau hálito*";
		else
			SetCooldown(who, "cebola", 5);
		endif
	endif
	
	var poisonInfo := GetObjProperty(food, "poisonInfo");
	var foodcharge := GetObjProperty(food, "foodcharge");
	var subtract;
	if (foodcharge)
		if (foodcharge > 0)
			subtract := "yes";
			SetObjProperty(food, "foodcharge", foodcharge-1);
		else
			subtract := SubtractAmount(food, 1);
		endif
	else
		subtract := SubtractAmount(food, 1);
	endif
	
	if(subtract)

		if (surprisemsg != "")
			SendSysMessageEx(who, ""+ surprisemsg, SSM_INFO);
		endif

		if (surpriseabove)
			printtext(who, ""+surpriseabove);
		endif

		hungerlvl := hungerlvl - foodvalue*60;
		case(RandomInt(3) + 1)
			1: PlaySoundEffect(who, 0x3b);
			2: PlaySoundEffect(who, 0x3c);
			3: PlaySoundEffect(who, 0x3d);
		endcase
		PerformAction(who, 0x22);
                
		hungerlvl  := cint(hungerlvl /60);
		if (hungerlvl <= 0)
			hungerlvl := 0;
		endif

		/*case(hungerlvl)
			0:  themsg := "você esta muito cheio";
			1:  themsg := "você esta muito cheio";
			2:  themsg := "você esta satisfeito";
			3:  themsg := "você se sente um pouco mais cheio depois de consumir a comida";
			4:  themsg := "você ainda esta com fome";
			5:  themsg := "você ainda esta muito faminto";
		endcase*/
		themsg := "Você se sente melhor alimentado";

		AP_SetVital(who, "Hunger", hungerlvl*60);

		SendSysMessageEx(who, themsg, SSM_INFO);

		if(poisonInfo)
			poisonInfo.dosage := poisonInfo.dosage - 1;
			if (poisonInfo.dosage <= 0) //se não tiver mais cargas de veneno, deleta
				EraseObjProperty(food, "poisonInfo");
				return 0;
			else //se ainda tiver atualiza na arma
				SetObjProperty(food, "poisonInfo", poisonInfo);
			endif

			var roll := rollResistDice(who, CONSTITUTION);
			if (checkImmunity(who, DMG_POISON) || roll >= poisonInfo.difficulty )
				return 0;
			endif

			TS_StartTimer(who, poisonInfo.name, poisonInfo.time, poisonInfo.level, who);
		endif
	endif
endprogram
