//$Id: use.src 128 2006-05-27 17:36:42Z austinheilman $

use uo;
use os;
use cfgfile;

include ":keys:key";
include ":traps:traps";
include ":charactercreation:feats";
include "include/say";

program useContainer(mobile, container)
	var item_cfg := ReadConfigFile(":*:itemdesc");
	item_cfg := item_cfg[container.graphic];
	
	if ( mobile.cmdlevel )
		SendSysMessageEx(mobile, "Command level is present - container is skipping locks, traps, etc.");
		SendViewContainer(mobile, container);
   elseif (GetObjProperty(container, "broken"))
		SendSysMessageEx(mobile, "Isso parece estar quebrado.");
      return 0;
   elseif (mobile.warmode && GetObjProperty(container, "breakable") && ContainerIsLocked(mobile, container) && Distance(mobile, container) <= 1)
      start_script(":tn:destroyitems", array{mobile, container});
      return 0;
	elseif ( GetObjProperty(container, "TrapList") != error )
      if (GetObjProperty(container, "trapSpoted") && HaveFeatSecondary(mobile, 48))
         start_script(":traps:disarmTrap", array{mobile, container});
         return 0;
      endif
		if (RunTraps(container, mobile))
         SetObjProperty(container, "trapSpoted", 1);
         PrintText(mobile, "*dispara armadilha*");
      endif
		return 0;
	elseif ( ContainerIsLocked(mobile, container) ) 
      SendSystemReport(container, "*Isto está trancado.*", SSM_FAIL, mobile);
		return 0;
	endif

   if (GetObjProperty(container, "treasure") && !GetObjProperty(container, "spawned_time"))
      var time_respawn := AddTimeTo(ReadGameClock(), 30+RandomDiceRoll("3d20"), "minutes");
      SetObjProperty(container, "spawned_time", time_respawn);
   endif
	
	SendViewContainer(mobile, container);
	
	if ( (item_cfg.OpenGraphic) && (item_cfg.CloseGraphic) )
		container.graphic := item_cfg.OpenGraphic;
		Sleep(2);
		container.graphic := item_cfg.CloseGraphic;
	endif
endprogram

function ContainerIsLocked(mobile, container)
	if ( !container.locked )
		// Nothing to worry about.
		return 0;
	elseif ( KP_HasKeyForLock(mobile, container) )
		SendSysMessageEx(mobile, "Você abre o container com a chave.");
		return 0;
	else
		return 1;
	endif
endfunction
