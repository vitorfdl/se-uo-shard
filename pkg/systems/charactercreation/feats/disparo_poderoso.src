use os;

include "include/say";
include ":timedscripts:timedScripts";
include ":ghaia:ghaiaUtil";
include ":fls_core:fls_characters";
include "include/client";
include "include/facings";


program startTalent(param)
   var who := param[1];
   var talent_name := param[2];
   var res := disparoPoderoso(who);
   if (!res)
      TS_LowerDuration(who, talent_name, -1, 1);
   endif
endprogram

function disparoPoderoso(param)
   var who := param[1];
	if (!who.weapon.isBow())
		SendSysMessageEx(who, "Você precisa estar usando um arco.", SSM_INFO);
		return 0;
	endif
	SendSysMessageEx(who, "Escolha a direção que iá disparar a flecha", SSM_INFO);
	var targ := TargetCoordinates(who);
	if (!who.warmode)
		who.warmode := 1;
	endif
	
	var cfg := ReadConfigFile(":combat:itemdesc");
	var elem := FindConfigElem(cfg, who.weapon.objtype);
	
	if (!CheckLOSAt(who, targ.x, targ.y, targ.z))
		SendSysMessageEx(who, "O alvo nao esta visivel para você.", SSM_FAIL);
		return 0;
	elseif (CoordinateDistance(who.x, who.y, targ.x, targ.y) > 13)
		SendSysMessageEx(who, "O alvo esta muito distante.", SSM_FAIL);
		return 0;
	elseif (IsBehind(who, targ.x, targ.y))
		SendSysMessageEx(who, "Você esta de costas para o alvo!", SSM_FAIL);
		return 0;
	endif
	
	var aljava := GetEquipmentByLayer(who, 0x14);
	var ammo := GetObjProperty(who.weapon, "ammo");
	if (!ammo)
		SendSysMessage(who, "Você não definiu o tipo de flecha que vai usar. [clique 2x no arco e na flecha]");
		return 0;
	elseif ( ConsumeSubstance( aljava, ammo, 1 ) == error)
		SendSysMessage(who, "Você não definiu o tipo de flecha que vai usar. [clique 2x no arco e na flecha]");
		return 0;
	endif
	
	PlaySoundEffect(who, 0x14A);
	Performaction(who, elem.Anim);
	PlayMovingEffectXYZ( who.x, who.y, who.z+3, targ.x, targ.y, targ.z, elem.ProjectileAnim, 10, 10, 0, who.realm);
	sleepms(35);
	PlayMovingEffectXYZ( who.x, who.y, who.z+3, targ.x, targ.y, targ.z, 0x377A, 6, 10, 0, who.realm);
	
	var distancia := GetCoordsInLine(who.x, who.y, targ.x, targ.y);
	
	//lista inimigos e aplica dano
	var alreadydamaged := array{};
	foreach pontos in distancia
		var mobiles := ListMobilesNearLocation(pontos.x, pontos.y, who.z, 1, who.realm);
		foreach mobile in mobiles
			if (!(mobile.serial in alreadydamaged) && (mobile != who) && !(mobile in who.party.members))
				alreadydamaged.Append(mobile.serial);
	         var basedamage := CalculateRawDamage(who, elem, who.weapon);
            if (alreadydamaged.size() < 2)
               basedamage += 2;
            endif
				DamageFLS(mobile, basedamage, DMG_PIERCING, who);
			endif
			sleepms(20);
		endforeach
		sleepms(20);
	endforeach

	who.warmode := 1;
	CreateItemAtLocation( targ.x, targ.y, targ.z, ammo, 1, who.realm);

   return 1;
endfunction
