
use uo;

include "include/say";
include ":taming:taming";
include ":gumps:yesno";
include ":attributes:attributes";
include ":timedScripts:timedScripts";
include "include/facings";
include "include/damage";
include ":tn:combat";

program tirodesfocado(who)
	SendSysMessageEx(who, "Escolha a direcao que ira disparar a flecha", SSM_INFO);
	var targ := TargetCoordinates(who);
	if (who.warmode == 1)
		who.warmode := 0;
	endif
	
	var cfg:=ReadConfigFile(":combat:itemdesc");
	var elem := FindConfigElem(cfg, who.weapon.objtype);
	
	if (!CheckLOSAt(who, targ.x, targ.y, targ.z))
		SendSysMessageEx(who, "O Alvo nao esta visivel para voce.", SSM_FAIL);
		return 1;
	elseif (CoordinateDistance(who.x, who.y, targ.x, targ.y) > 13)
		SendSysMessageEx(who, "O Alvo esta muito distante.", SSM_FAIL);
		return 1;
	elseif (IsBehind(who, targ.x, targ.y))
		SendSysMessageEx(who, "Voce esta de costas para o alvo!", SSM_FAIL);
		return 1;
	elseif (lower(elem.Attribute) != lower("RangedCombat"))
		SendSysMessageEx(who, "Voce precisa estar usando um arco.", SSM_FAIL);
		return 1;
	endif
	
	var aljava := GetEquipmentByLayer(who, 0x14);
	var ammo := GetObjProperty(who.weapon, "ammo");
	if (!ammo)
		SendSysMessage(who, "Voce nao definiu o tipo de flecha que vai usar. [clique 2x no arco e na flecha]");
		return 1;
	elseif ( ConsumeSubstance( aljava, ammo, 1 ) == error)
		SendSysMessage(who, "Voce nao definiu o tipo de flecha que vai usar. [clique 2x no arco e na flecha]");
		return 1;
	endif
	
	PlaySoundEffect(who, 0x235);
	Performaction(who, elem.Anim);
	PlayMovingEffectXYZ( who.x, who.y, who.z+3, targ.x, targ.y, targ.z, elem.ProjectileAnim, 10, 10, 0, who.realm);
	sleepms(35);
	PlayMovingEffectXYZ( who.x, who.y, who.z+3, targ.x, targ.y, targ.z, 0x377A, 6, 10, 0, who.realm);
	
	var distancia := GetCoordsInLine(who.x, who.y, targ.x, targ.y);
	
	//calculo damage
	var dmgList := dictionary;
	dmgList := getNewDamageList(who);
	var basedamage := CalculateRawDamage(who, who.weapon);
	
	var bonus := cdbl(0.2+(Ap_GetSkill(who, TACTICS) / 100));
	
	//lista inimigos e aplica dano
	var alreadydamaged := array{};
	foreach pontos in distancia
		var mobiles := ListMobilesNearLocation(pontos.x, pontos.y, who.z, 1, who.realm);
		foreach mobile in mobiles
			if (!(mobile.serial in alreadydamaged) && (mobile != who) && !(mobile in who.party.members))
				alreadydamaged.Append(mobile.serial);
				var total := cint(CalculateTotalDamage(mobile, basedamage, who, dmgList));
				DamageFLS(mobile, cint(total+(total*bonus)), DMG_FORCED, who);
			endif
			sleepms(20);
		endforeach
		sleepms(20);
	endforeach

	who.warmode := 1;
	CreateItemAtLocation( targ.x, targ.y, targ.z, ammo, 1, who.realm);
endprogram 
