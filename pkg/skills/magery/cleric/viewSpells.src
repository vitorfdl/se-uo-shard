use uo;
use os;

include "include/client";
include "include/sysEvent";
include ":charactercreation:characters";
include ":magery:tnmagery";

const SPELL_BTN := 200;

program clericPowerWords(who)
	detach();
   if (!who.Caracteristica("Clérigo de Anar"))
      SendSysMessageEx(who, "Este menu não e acessivel para você.", SSM_FAIL);
      return;
   endif

   CloseGump(who, GUMPID_CLERICSPELL);

   var char_level   := who.GetLevel();
   var cleric_slots := AP_GetTrueSkill(who, MILAGRE_DE_CURA) + AP_GetTrueSkill(who, MILAGRE_DE_NECROMANCIA) + 1;
   if (who.Caracteristica("Dádiva de Anar'Niel"))
      cleric_slots += 1;
   endif
   var cleric_list  := GetObjProperty(who, "cleric_list");
   if (!cleric_list) cleric_list := array{}; endif
   // cleric_list := array{ struct{ "id":= 1, "used" := 0, "time" := 0} } + cleric_list;
   var slots_used := 0;
   foreach spell in cleric_list
      var new_spell := spell.id;
      WriteSpellInfo(new_spell, ":magery:config/cleric");
      new_spell.+used := spell.used;
      new_spell.+time := spell.time;
      cleric_list.Erase(_spell_iter);
      cleric_list.Insert(_spell_iter, new_spell);

      if (new_spell.id != 1)
         slots_used += spell.circle;
      endif

      sleepms(2);
   endforeach

   var available_slots := cleric_slots - slots_used;

   var canceled := 0;
   while (!canceled)
      var input := CreateClericGump(who, cleric_list, available_slots);

      if (!input[0])
         canceled := 1;
      elseif (input[0] >= 200)
         var slot := input[0] - 200 - 1;
         cleric_list := GetObjProperty(who, "cleric_list");
         cleric_list.Erase(slot);
         SetObjProperty(who, "cleric_list", cleric_list);
         SendSysMessageEx(who, "Você abandonou o milagre.", SSM_INFO);
         canceled := 1;
      endif
   endwhile
endprogram

function CreateClericGump(who, cleric_list, available_slots)
	var gump := GFCreateGump();
   
   var size := cleric_list.Size() + available_slots;
   if (size >= 1)
      size := size * 26;
   else
      size := 0;
   endif

   GFSetId(gump, GUMPID_CLERICSPELL);
	GFClosable(gump, 1);
	GFResizePic(gump, 10, 26, 9390, 476, 120+size);
	GFHTMLArea(gump, 180, 65, 192, 20, "<BASEFONT Size=5 Color=#666666 >Lista de Bençãos", 0, 0); //shadow
	GFHTMLArea(gump, 182, 67, 192, 20, "<BASEFONT Size=5 Color=#666666 >Lista de Bençãos", 0, 0);
	GFHTMLArea(gump, 181, 66, 192, 20, "<BASEFONT Size=5 Color=#ffffff >Lista de Bençãos", 0, 0); //shadow
	GFGumpPic(gump, 139, 84, 1469);
	// GFTextLine(gump, 187, 66, 0, "Lista de Bençãos");
	GFGumpPic(gump, 31, 98, 9773, 0);
	GFTextLine(gump, 56, 95, 1445, "Bençãos");
	GFTextLine(gump, 255, 96, 1445, "Palavras de Poder");

   for i := 1 to available_slots
      cleric_list.append(struct{});
   endfor

   var y := 116;
   foreach spell in cleric_list
      var not_empty := isNotEmpty(spell);
      var spellname := "<basefont Size= 1>Empty";
      var powerwords := "";

      if (spell.id)
         spellname := spell.name;
         powerwords := ", ".join(spell.powerwords);
      endif

      var color := "";
      if (not_empty == 3 || not_empty == 1)
         color := "<BASEFONT color=#b20000>";
         powerwords := "{}Milagre usado.".format(color);
      endif

      GFHTMLArea(gump, 52, y, 192, 24, "{}{}".format(color, spellname), 1, 0);
      if (spell.time)
         var time := GetTimeBetween(polcore().systime, spell.time, "minutes");
         if (time > 0)
            GFToolTipText(gump, "Pode esquecer em: {} minuto(s)".format());
         endif
      endif

      GFHTMLArea(gump, 253, y, 185, 24, "{}".format(powerwords), 1, 0);

      if (not_empty == 0 || not_empty == 3)
         GFAddButton(gump, 442, y+2, 22150, 22151, GF_CLOSE_BTN, SPELL_BTN+_spell_iter);
         GFToolTipText(gump, "Esquecer milagre");
      endif

      y += 26;
      sleepms(2);
   endforeach

   return GFSendGump(who, gump);
endfunction

function isNotEmpty(spell)
   if (spell.id == 1 || !spell.time)
      return 2; //cannot remove
   elseif(spell.time < polcore().systime && spell.used)
      return 3; // can remove and already used
   elseif (spell.time < polcore().systime)
      return 0; //can remove
   elseif(spell.used)
      return 1; // already used
   endif

   return 2; //cannot remove
endfunction
