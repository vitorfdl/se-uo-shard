
use uo;
use os;

include "include/client";
include "include/sysEvent";
include "include/say";
include ":magery:tnmagery";

program clericPowerWords(parms)
   detach();
   var who := parms[1];

   var pid := GetPid();
   SetObjProperty(who, "#clericpid", pid);

   RegisterForSpeechEvents( who, 3 );
   EnableEvents( SYSEVENT_SPEECH, 1 );
   
   var event;
   while(who.connected)
      event := wait_for_event( 120 );
      if ( event && !who.dead && event.source ==  who && event.type == SYSEVENT_SPEECH)
         var cleric_list := GetObjProperty(who, "cleric_list");
         if (!cleric_list) cleric_list := array{}; endif;
         var auto := 1;
         WriteSpellInfo(auto, ":magery:config/cleric");
         cleric_list := array{auto} + cleric_list;

         if (cleric_list.size() < 1 || GetObjProperty(who, "#Casting"))
            continue;
         endif

         var spell_id := 0;
         foreach spell in cleric_list
            var verified := 0;
            if (!spell.powerwords[1] || spell.used)
               continue;
            endif

            var text := Lower(CChrz(event.uc_text));
            // Check if powerwords passed
            foreach powerword in (spell.powerwords)
               if (Lower(text).find(Lower(powerword)))
                  verified += 1;
               endif
               sleepms(2);
            endforeach

            // if all passed, set the spell_id
            if (verified == spell.powerwords.size())
               spell_id := spell.id;
               break;
            endif
            sleepms(2);
         endforeach

         if (spell_id)
            Start_Script(":magery:cast_spell", {who, spell_id, 0, 0, ":magery:config/cleric"});
         endif
      endif
      sleepms(10);
   endwhile
endprogram 
