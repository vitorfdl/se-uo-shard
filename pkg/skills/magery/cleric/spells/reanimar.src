use uo;
use os;
use vitals;

include "include/sounds";
include "include/say";
include ":magery:tnmagery";

program SpellScript(params)
	var who := params[1];
	var targ := params[3];
	params := 0; // No longer needed
	
	if  ( !targ.isa(POLCLASS_CORPSE) )
		SendSysMessageEx(who, "Isto nao pode ser alvejado por essa magia.", SSM_FAIL);
		return;
	endif

	if (GetObjProperty(targ, "morto") != error)
		SendSysMessageEx(who, "O alvo ja esta morto.", SSM_FAIL);
		return;
	endif
	
	PrintTextPrivate(who, "'Não se mova até o fim da conjuração!'", who, SSM_INFO);
	
	var oldx := who.x;
	var oldy := who.y;
	var time := 1;
	PerformAction(who, ANIM_CAST_AREA);
	var gliphs := array{};
	CreateRandomGliph(gliphs, targ.x-1, targ.y, targ.z);
	sleep(1);
	while  (time <= 3)
		if (oldx != who.x || oldy != who.y)
			PrintTextPrivate(who, "'Magia Cancelada!'", who, SSM_FAIL);
			break;
		endif
		
		if(targ.dead)
			PrintTextPrivate(who, "'Magia Cancelada!'", who, SSM_FAIL);
			break;
		endif
		
		if (time == 1)
			CreateRandomGliph(gliphs, targ.x, targ.y-1, targ.z);
		elseif (time == 2)
			CreateRandomGliph(gliphs, targ.x+1, targ.y, targ.z);
		else
			CreateRandomGliph(gliphs, targ.x, targ.y+1, targ.z);
		endif
		
		PerformAction(who, ANIM_CAST_AREA);
		time+=1;
		sleep(1);
	endwhile
	
	if (time >= 4)
		PlaySoundEffect(targ, SFX_SPELL_HEAL);
		if ( !GetObjProperty(targ, "npctemplate") )
			SetObjProperty(targ, "curado", 1);
		else
			var curado := 1;
			if ( !GetObjProperty(targ, "owner") )
				SendSysMessageEx(who, "O alvo esta morto.", SSM_FAIL);
				curado := 0;
			elseif (GetCooldown(targ, "resurrect") > 0 )
				if (!GetObjProperty(targ, "companheiro"))
					SendSysMessageEx(who, "O alvo esta morto.", SSM_FAIL);
					SetObjProperty(targ, "morto", 1);
					curado := 0;
				endif
			endif
			
			if (curado)
				ghaiaRessurect(targ);
			endif			
		endif
	endif
	
	sleep(1);
	foreach gliph in gliphs
		var destro := DestroyItem(gliph);
	endforeach
	return 1;
endprogram