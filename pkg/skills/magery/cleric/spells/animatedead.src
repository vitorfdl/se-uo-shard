use uo;
use os;

include "include/client";
include "include/sounds";
include "include/say";
include ":timedscripts:timedScripts";
include ":attributes:attributes";
include ":tn:npcutil";
include ":ghaia:ghaia";
include ":magery:basicMagery";


program SpellScript(params)
	var who := params[1];
	var targ := params[3];
	params := 0; // No longer needed

	var npctemplate := GetObjproperty(targ, "npctemplate");
   var npc_info := NPC_GetNPCConfig(targ.npctemplate);
	if ( npc_info.Category in array{"morto-vivo", "fada", "elemental"}  )
		SendSysMessageEx(who, "Não resta energia vital nesse corpo para ser reanimado", SSM_FAIL);
		return 0;
	elseif (getUndeads(who).size() >= MAX_UNDEAD)
		SendSysMessageEx(who, "Voce ja levantou corpos demais para sua capacidade", SSM_FAIL);
		return 0;
	elseif ( !targ.isa(POLCLASS_CORPSE) )
		SendSysMessageEx(who, "Voce so pode usar esta magia em corpos", SSM_FAIL);
		return 0;
	elseif (!GetObjproperty(targ, "npctemplate"))
		SendSysMessageEx(who, "Essa magia nao pode ser usada em players", SSM_FAIL);
		return 0;
	endif

   var level := Ceil(AP_GetTrueSkill(who, MILAGRE_DE_NECROMANCIA) / 2);
   var npclevel := cdbl(npc_info.Level);

   if (level < 2 && npclevel > 1.5)
		SendSysMessageEx(who, "Seu milagre não é poderoso o suficiente para levantar esta criatura.", SSM_FAIL);
		return 0;
   elseif (level < 3 && npclevel > 2)
		SendSysMessageEx(who, "Seu milagre não é poderoso o suficiente para levantar esta criatura.", SSM_FAIL);
		return 0;
   elseif (level < 4 && npclevel > 4)
		SendSysMessageEx(who, "Seu milagre não é poderoso o suficiente para levantar esta criatura.", SSM_FAIL);
		return 0;
   elseif (level < 5 && npclevel > 6)
		SendSysMessageEx(who, "Seu milagre não é poderoso o suficiente para levantar esta criatura.", SSM_FAIL);
		return 0;
   endif

	
	//Efeito da magia
	PlaySoundEffect(targ, SFX_SPELL_HEAL);
	CreateItemAtLocation(targ.x, targ.y, targ.z, 4650, 1, targ.realm);
	CreateItemAtLocation((targ.x - 1), targ.y, targ.z, 4650, 1, targ.realm);
	sleepms(500);
	CreateItemAtLocation(targ.x, (targ.y - 1), targ.z, 4650, 1, targ.realm);	
	sleepms(500);
	PlaySoundEffect(targ, 383);
	CreateItemAtLocation((targ.x + 1), targ.y, targ.z, 4650, 1, targ.realm);
	sleepms(500);
	CreateItemAtLocation(targ.x, (targ.y - 1), targ.z, 4650, 1, targ.realm);
	sleepms(500);
	var brilho := CreateItemAtLocation(targ.x, targ.y, (targ.z - 7), 14154, 1, targ.realm);
	sleepms(2000);
	Destroyitem( brilho );


	//Random de Monstros
	var undead := createnpcfromtemplate(npctemplate, targ.x, targ.y, targ.z, 0 , targ.realm);
	PlaySoundEffect(targ, 383);
	Destroyitem( targ );

   undead.color     := 2301;
   undead.truecolor := 2301;
   undead.run_speed := 5;
   undead.CA        := undead.CA - 4;
   SetObjProperty(undead, "dmgbonus", -2);
   var hitsmod := cint(GetObjproperty(undead, "hitsmod"));
   SetObjProperty(undead, "hitsmod", hitsmod - cint(cdbl(AP_GetVitalMaximumValue(undead, HITS)) * 0.2));
	undead.SetMaster(who);
	SetObjProperty(undead, "GuardKill", 1);	
	SetObjProperty(undead, "owner", who.serial);
	SetObjProperty(undead, "name", targ.name);
	SetObjproperty(undead, "script", targ.script);
   SetObjProperty(undead, "summoned", 1);
	SetObjProperty(undead, "AntigoTreinadorID", who.serial);

	undead.script := ":ghaia:follower";
	RestartScript(undead);
   var petname := undead.name;
   Run_Script_To_Completion(":death:npcdeath", {undead});
   SetName(undead, petname);
	AddUndead(who, undead);
	TS_StartTimer(undead, "summon", 250 + (level * 10));
	
	SendSysMEssageEx(who, "Você teve sucesso ao levantar o corpo", SSM_INFO);
	
	return 1;
endprogram 
