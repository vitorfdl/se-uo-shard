use uo;
use cfgfile;
use util;
use os;

var spellcfg := ReadConfigFile(":magery:config/mage");

program scroll(item)
	var last_objtype;
	while (item)
		if (last_objtype != GetObjProperty(item, "spell_id"))
			scrollCreate(item);
			last_objtype := GetObjProperty(item, "spell_id");
		endif
		sleep(10);
	endwhile
endprogram

function scrollCreate(item)
	if (GetObjProperty(item, "spell_id") != error)
		setGraphic(item);
		return 1;
	endif

	//pega o level do scroll
	var scrollcfg := ReadConfigFile(":magery:itemdesc");
	var elem      := FindconfigElem(scrollcfg, item.objtype);
	var lvl       := GetConfigInt(elem, "Circle" );
	var spelltype := GetConfigString(elem, "SpellType" );
	if (!lvl)
		lvl := 1;
	endif

	//pega todas magias do cfg com esse level e armazena no array
	var keys := GetConfigIntKeys(spellcfg);
	var randomspells := array;
	foreach key in keys
		var spelllvl := GetConfigInt( FindconfigElem(spellcfg, key), "Circle" );
		var type     := GetConfigString( FindconfigElem(spellcfg, key), "Type" );
		if (spelllvl == lvl && spelltype == type)
			randomspells.append(key);
		endif
	endforeach

	var spell_id := randomspells.randomEntry();
	SetObjProperty(item, "spell_id", spell_id);

	return 1;
endfunction

function setGraphic(item)
	var spell_id := GetObjProperty(item, "spell_id");
	var type     := GetConfigString( FindconfigElem(spellcfg, spell_id), "Type" );
	case (type)
		"Fogo": item.graphic := 0x2DAC;
		"Gelo": item.graphic := 0x2DA2;
		"Eletrecidade": item.graphic := 0x2DA7;
		"Veneno": item.graphic := 0x2DA4;
		"Comando": item.graphic := 0x2DA9;
	endcase
endfunction