use uo;

include ":gumps:gumps";
include ":gumps:yesno";
include ":gumps:requestgump";
include ":tn:tngumps";
include ":magery:tnmagery";
include "include/say";
include "include/utils";
include ":attributes:attributes";
include ":containers:containers";
include ":itemutils:itemdesc";
include "include/client";

include "include/facings";
include ":tn:cooldown";
include ":timeUtils:time";

program book(who, book)
	var can_read  := HaveFeat(who, "Linguagem Élfica");
   var can_trip;
   if (HaveFeat(who, "Truque de Magia"))
      var last_change := cint(GetObjProperty(who, "cantrip_lastchange"));
      if (!GetObjProperty(who, "cantrip_spell") || last_change <= polcore().systime)
         can_trip := 1;
      endif
   endif

	var book_gump := CreateBookGump(book, can_read, can_trip);
	var result    := GFSendGump(who, book_gump);

   if (result[0] > 500)
		var spell := result[0] - 500;
		var spells := GetObjProperty(book, "spells");
      if (spells[spell])
         var newtime := AddTimeTo(GetTime(), 3, "days");
         SetObjProperty(who, "cantrip_lastchange", newtime);
         SetObjProperty(who, "cantrip_spell", spells[spell]);
         SendSysMessageEx(who, "Truque mágico definido, você poderá escolher outro em 3 dias.", SSM_INFO);
      endif
	elseif (result[0] > 300)
		var spell := result[0] - 300;

		var spells := GetObjProperty(book, "spells");
	   var powered_list := GetObjProperty(book, "spells_powered");
		//sendsysmessage(who, " " + bookopt);

		if (spells[spell])
			CopyRecipe(who, spells[spell], powered_list[spells[spell]]);
		endif
	elseif (result[0] > 100)
		var spell    := result[0] - 100;
		var spells   := GetObjProperty(book, "spells");
		var serialid := GetObjProperty(book, "serialid");
		if (serialid != error && serialid != who.serial)
			SendSysMessageEx(who, "Você não entende as anotacoes do livro.", SSM_FAIL);
			return 1;
		elseif (!spells[spell])
			SendSysMessageEx(who, "Algum erro ocorreu com sua magia, avise um GM.", SSM_FAIL);
			return 1;
		endif
	   var powered_list := GetObjProperty(book, "spells_powered");
      var power := cint(powered_list[spells[spell]]);
      if (power <= 1) power := 1; endif
		WriteMagic(who, spells[spell], power);
	endif

endprogram

Const OBJ_BLANK_SCROLL := 0xE34;
Const OBJ_RECIPE_SLIP := 0x6116;

function CopyRecipe(who, recipe, power)
	WriteSpellInfo(recipe);
	//sendsysmessage(who, " " + spellinfo + " " + recipe + "  " + cint(recipe));
	if (!SearchArrayOfModel("objtype", cint(0x6400), EnumerateItemsInContainer(who.backpack))) 
		SendSysMessageEx(who, "Você precisa de uma pena e tinteiro", SSM_FAIL);
		return;
	elseif (!ConsumeSubstance(who.backpack, OBJ_BLANK_SCROLL, 1))
		SendSysMessageEx(who, "Você precisa de um pergaminho em branco.", SSM_FAIL);
		return 0;
	endif

	if (rollAttrDice(who, WISDOM) <= recipe.difficulty+6)
	   SendSysMessageEx(who, "Você copiou o pergaminho sem cuidados e ele ficou ilegivel.", SSM_FAIL);
		return;
	endif 
   
   var item := createItemInBackpack(who, OBJ_RECIPE_SLIP, 1);
   if (item == error)
      sendsysmessage(who, "  " + item);
   endif
   
   printtext(who, "*escrevendo pergaminho*");
   if (!Wait(who, 3))
      SendSysMessageEx(who, "Você se moveu e parou de copiar o pergaminho", SSM_FAIL);
      return;
   endif
   printtext(who, "*escrevendo pergaminho*");

   SetObjProperty(item, "spell_id", recipe.id);
   SetObjProperty(item, "spell_power", power);
   SendSysMessageEx(who, "Você fez uma cópia do pergaminho.", SSM_INFO);
endfunction


function CreateBookGump(book, can_read := 0, can_trip := 0)
	var spell_list := GetObjProperty(book, "spells");
	var powered_list := GetObjProperty(book, "spells_powered");
	if( TypeOf(spell_list) != "Array" )
		spell_list := array{};
		SetObjProperty(book, "spells", spell_list);
	endif

	if( TypeOf(powered_list) != "Dictionary" )
		powered_list := dictionary{};
		SetObjProperty(book, "spells_powered", powered_list);
	endif

	var book_gump := GFCreateGump();
	GFSetId(book_gump, GUMPID_GRIMORIO);
// Begin Page 0 Setup
	GFGumpPic(book_gump, 100, 10, 2200);
	GFGumpPic(book_gump, 125, 50, 57);
	GFGumpPic(book_gump, 145, 50, 58);
	GFGumpPic(book_gump, 160, 50, 58);
	GFGumpPic(book_gump, 175, 50, 58);
	GFGumpPic(book_gump, 190, 50, 58);
	GFGumpPic(book_gump, 205, 50, 58);
	GFGumpPic(book_gump, 220, 50, 58);
	GFGumpPic(book_gump, 230, 50, 59);
	GFGumpPic(book_gump, 290, 50, 57);
	GFGumpPic(book_gump, 310, 50, 58);
	GFGumpPic(book_gump, 325, 50, 58);
	GFGumpPic(book_gump, 340, 50, 58);
	GFGumpPic(book_gump, 355, 50, 58);
	GFGumpPic(book_gump, 370, 50, 58);
	GFGumpPic(book_gump, 385, 50, 58);
	GFGumpPic(book_gump, 395, 50, 59);

// Begin Page 1 Setup
	GFPage(book_gump, 1);
	//GFAddButton(book_gump, 125, 15, 2472, 2473, GF_CLOSE_BTN, 50);
	GFHTMLArea(book_gump, 138, 26, 130, 18, "<i>Livro de Magia</i>");
	GFAddButton(book_gump, 393, 14, 2206, 2206, GF_PAGE_BTN, 2);

	var pOX       := 130;
	var pOY       := 65;
	foreach spell in spell_list
		GFAddButton(book_gump, pOX, pOY, 2103, 2104, GF_CLOSE_BTN, 100+_spell_iter);
		pOY+=15;
		if (pOY >180)
			pOY := 65;
			pOX := 290;
		endif
      sleepms(2);
	endforeach
// End Page 1 Setup

	pOX      := 145;
	pOY      := 60;

   var i;
	for (i := 1; i <= 16; i += 1)
      var side := 1;
      if (i % 2 == 0)
         side := 2;
      endif

      if (side == 2)
         if(i != 9)
            GFAddButton(book_gump, 393, 14, 2206, 2206, GF_PAGE_BTN, book_gump.cur_page+1);
         endif
      else
         GFPage(book_gump, book_gump.cur_page+1);
         GFAddButton(book_gump, 125, 14, 2205, 2205, GF_PAGE_BTN, book_gump.cur_page-1);
      endif
      
		GenerateFilledSide(book_gump, spell_list, book_gump.cur_page, side, pOX, pOY, i, can_trip, powered_list);

		SleepMS(2);
	endfor

	return book_gump;
endfunction

//----------------------------
// Generate every page of the book. And the button for the First page.
// Inside generate empty page too
//-----------------
function GenerateFilledSide(byref book_gump, spell_list, i, side, byref pOX, byref pOY, spell_pos, can_trip, powered_list)
	var sOX := 160;
   var difX := 0;
	if (side == 2)
		sOX := 300;
      difX := 20;
	endif
   // print(spell_pos);
	var spell_entry := spell_list[spell_pos];
   var powered := cint(powered_list[spell_entry]);
   if (!powered) powered := 1; endif

	WriteSpellInfo(spell_entry);
	spell_entry.description := "<BASEFONT COLOR=#000066>{}<br>{}<br><br>".format(cstr(spell_entry.description), cstr(spell_entry.flavortext));
   spell_entry.description += "<BASEFONT COLOR=#330000>Círculo: {}<br> Mana: {}<br> Cargas: {}".format(spell_entry.circle, spell_entry.mana, cint(spell_entry.charges) * powered);
	// var regkeys := spell_entry.reagents.keys();
	// foreach r in (regkeys)
	// 	spell_entry.description := "{}{} {}<br>".format(spell_entry.description, cstr( r ), cstr(spell_entry.reagents[cstr(r)]));
   //    sleepms(2);
	// endforeach
	if( TypeOf(spell_entry) == "Struct" )
		if( spell_entry.name )
         spell_entry.name += " {}".format(convertToRoman(powered));
			// This puts the rune in page 1's list.
			GFPage(book_gump, 1);
			GFTextCrop(book_gump, pOX,  pOY, 115, 17, 995, spell_entry.name);
			GFPage(book_gump, i);

			GFTextCrop(book_gump, sOX,  15, 100, 18, 995, spell_entry.name);
			GFTextCrop(book_gump, sOX+50+difX, 35, 100, 18, 995, "Copiar");
			GFAddButton(book_gump, sOX+35+difX, 39, 2103, 2104,  GF_CLOSE_BTN, 300+spell_pos);

         if (can_trip && spell_entry.circle <= 1)
            GFTextCrop(book_gump, sOX-10+difX, 35, 100, 18, 995, "Truque");
            GFAddButton(book_gump, sOX-25+difX, 39, 2103, 2104,  GF_CLOSE_BTN, 500+spell_pos);
         endif
			//Fix description position by side
			if (side == 1)
				GFHTMLArea(book_gump, sOX-20, 60, 120,  140, cstr(spell_entry.description), 0, 1);
			else
				GFHTMLArea(book_gump, sOX, 60, 120,  140, cstr(spell_entry.description), 0, 1);
			endif

		endif
	else
		GenerateEmptySide(book_gump, side, pOX, pOY, i);
	endif

	pOY := pOY + 15;
	if( pOY > 165 )
		pOY := 60;
		pOX := 305;
	endif
endfunction

function GenerateEmptySide(byref book_gump, side, pOX, pOY, page_num)
	if( side == 1)
		// This puts the rune in page 1's list.
		GFPage(book_gump, 1);
		GFTextCrop(book_gump, pOX,  pOY, 115, 17, 10, "");
		GFPage(book_gump, page_num);
		GFTextCrop(book_gump, 145, 60, 115, 17, 10, "");
	else
		// This puts the rune in page 1's list.
		GFPage(book_gump, 1);
		GFTextCrop(book_gump, pOX,  pOY, 115, 17, 10, "");
		GFPage(book_gump, page_num);
		GFTextCrop(book_gump, 305, 60, 115, 17, 10, "");
	endif
endfunction
