/* $Id$
 *
 */
use uo;
use os;
use cfgfile;

include "include/say";
include "include/arrays";

//tipo do book eh pra saber se pode inserir scrolls de um certo tipo em um certo book
function GetBookType(book)
	var type := GetObjProperty(book, "Booktype");
	if (type == error)
		return "mage";
	else
		return type;
	endif

endfunction 

program on_insert(who, book, movetype, inserttype, item, existing_stack, amt);
	amt            := amt;
	existing_stack := existing_stack;
	movetype       := movetype;
	inserttype     := inserttype;

	if( book.movable == 0 )
		SendSysMessage(who, "Cancelado.");
		MoveItemToContainer(item, who.backpack);
		return 0;
	elseif( !ReserveItem(book) || !ReserveItem(item) )
		MoveItemToContainer(item, who.backpack);
		SendSysMessage(who, "O livro esta em uso.");
		return 0;
	endif

	addScroll(who, book, item);
	return 1;
endprogram

function addScroll(who, book, scroll)
	var spell_id    := GetObjProperty(scroll, "spell_id");
	var spell_list := GetObjProperty(book, "spells");
	if (spell_list == error)
		spell_list := array;
	endif
	
	if (!spell_id)
		SendSysMessage(who, "Este nao parece um pergaminho de magia.");
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif( spell_list.Size() > 15 )
		SendSysMessage(who,"Este livro ja esta cheio.");
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif( !ReserveItem(scroll) )
		SendSysMessage(who,"Cancelado");
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	elseif (FindInArray(spell_list, spell_id))
		SendSysMessageEX(who,"O grimorio ja possui esta magia.", SSM_FAIL);
		MoveItemToContainer(scroll, who.backpack);
		return 0;
	endif
	
	Set_Critical(1);

	spell_list.Append(spell_id);

	SetObjProperty(book, "spells", spell_list);

	SendSysMessage(who, "Voce adicionou o pergaminho no livro.");
	ReleaseItem(scroll);
	DestroyItem(scroll);
	Set_Critical(0);

	return 1;
endfunction
