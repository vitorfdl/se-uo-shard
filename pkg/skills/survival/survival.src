// $Id: tracking.src 916 2005-11-05 23:44:32Z austin $

use uo;
use basic;
use os;
use util;
use cfgfile;

include "include/client";
include ":attributes:attributes";
include ":charactercreation:habilidades";
include ":fls_core:fls_characters";

include "include/say";
include ":itemutils:canAccess";
include ":attributes:include/attributeConstants";
include ":attributes:include/attributes";

include "include/objtype";

include "include/sysEvent";
include ":gumps:gumps";
include ":gumps:gumps_ex";
include ":gumps:requestgump";

var BKGND_SILVERTRIM := GFCfgConst("Defaults", "BackGround");


program tracking(who)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");

	InitGump(who);

endprogram

function InitGump(who)

	var gump := GFCreateGump(50, 25);
	GFResizePic(gump, 0, 25, 0x13BE, 300, 200);
	GFResizePic(gump, 15, 40, 0xBB8, 270, 170);
//	GFResizePic(gump, 2, 0, BKGND_SILVERTRIM, 300, 200);
	GFTextLine(gump, 60, 50, 600, "Escolha o tipo de Rastreamento" );
	GFTextLine(gump, 47, 80, 800, "Rastreamento de Criaturas");
	GFAddButton(gump, 30, 84,  2103, 2104, GF_CLOSE_BTN, 1 );
	GFTextLine(gump, 47, 100, 800, "Rastreamento de Itens");
	GFAddButton(gump, 30, 104,  2103, 2104, GF_CLOSE_BTN, 2 );

	var ifres := GFSendGump(who, gump);	

	if (ifres[0] != 0)
		if (ifres[0] == 1)
			MobileTracking(who);	
		endif
		if (ifres[0] == 2)
			ItemTracking(who);
		endif
	endif
endfunction


function MobileTracking(who)
	var roll := rollAttrDice(who, WISDOM) + getProficiencyBonus(who, SOBREVIVENCIA);
   var tracker := ReadGameClock();
   SetObjProperty(who, "TrackingCounter", tracker);
   var range := 18;

   var nmobiles := ListMobilesNearLocationEX(who.x,who.y, LIST_IGNORE_Z, range, 0x03, who.realm);
   var mobiles := array;

   foreach mob in nmobiles
      var diff := rollAttrDice(mob, DEXTERITY) + getProficiencyBonus(mob, FURTIVIDADE);
      if ( mob != who && roll > diff )
         mobiles.append(mob);
      endif
      sleepms(2);
   endforeach

   PrintText(who, "*Estudando o local a sua volta*");

   if(len(mobiles) < 1)
      SendSysMessageEx(who, "Não encontrou nenhum rastro ao redor para ser perseguido.", 3, 660);
      return;
   endif

   who.SetAchievement("mob_track", 1);
   TrackGump(who, mobiles);
endfunction


function ItemTracking(who)
	var roll := rollAttrDice(who, WISDOM) + getProficiencyBonus(who, SOBREVIVENCIA);
   SetObjProperty(who, "TrackingCounter", ReadGameClock());
   var range := 15;
   var items :=  listitemsnearlocation(who.x, who.y, LIST_IGNORE_Z, range, who.realm);
   var items_found := array{};

   foreach item in items
      var diff := Distance(item, who) / 2;
      if (diff < 8)
         diff := 8;
      endif

      if ( roll > diff )
         items_found.append(item);
      endif
      sleepms(2);
   endforeach

   PrintText(who, "*Estudando o local a sua volta*");

   if(len(items_found) < 1)
      SendSysMessageEx(who, "Você não encontrou traços de item algum.", 3, 660);
      return;
   endif

   who.SetAchievement("item_track", 1);
   ItemTrackGump(who, items_found);
endfunction



//gump da lista de mobs
function TrackGump(who, mobiles)
	var gump := GFCreateGump(50, 25);

	var mobdesc;
	var x,y,b;
	var mobarray := array;
	x := 0;
	y := 0;
	b := 1;
//	GFResizePic(gump, 2, 0, BKGND_SILVERTRIM, 600, 500);
	GFResizePic(gump, 0, 25, 0x13BE, 630, 530);
	GFResizePic(gump, 15, 40, 0xBB8, 600, 500);
	GFTextLine(gump, 200, 45, 600, "Escolha o alvo a ser rastreado:" );
	GFPage(gump, 1);
	var colunas := 1;
	var pages := 1;
	foreach mobile in mobiles
     	if((mobile != who) && (!mobile.dead))
         mobdesc := GetMobDesc(who, mobile);
         mobarray.append(mobdesc);
         GFTextLine(gump, (47 + x), (70 + y), ""+MobDistanceColor(who, mobile), mobdesc);
         GFAddButton(gump, (30 + x), (74 + y),  2103, 2104, GF_CLOSE_BTN, b );
         y := y + 20;
         b := b + 1;
         if (y > 380)
            x := x + 150;
            y := 0;
            colunas := colunas + 1;
         endif
         if (colunas == 5) //adiciona uma page
            colunas := 0;
            x := 0;
            y := 0;
            pages := pages + 1;
            GFAddButton(gump, 570, 520, 2224, 2224, GF_PAGE_BTN,  pages);
            GFPage(gump, pages);
            GFAddButton(gump, 30, 520, 2223, 2223, GF_PAGE_BTN,  pages-1);
         endif
	   endif
	endforeach

	var ifres := GFSendGump(who, gump);	

	if (ifres[0] != 0)
		var mobontrack := mobiles[ifres[0]];
		var mobname := mobarray[ifres[0]];
		SendSysMessageEx(who, "Você agora esta seguindo os rastros de um(a) " + mobname + mobontrack.x +mobontrack.y, 3, 191);
		CreatureTrack(who, mobontrack,mobname);
	endif
endfunction


//lista os corpos e acampamentos
function ItemTrackGump(who, items)

	var gump := GFCreateGump(50, 25);

	var x,y,b;
	var itemarray := array;
	x := 0;
	y := 0;
	b := 1;
//	GFResizePic(gump, 2, 0, BKGND_SILVERTRIM, 600, 500);
	GFResizePic(gump, 0, 25, 0x13BE, 630, 530);
	GFResizePic(gump, 15, 40, 0xBB8, 600, 500);
	GFTextLine(gump, 200, 45, 600, "Escolha o evento/item a ser rastreado:" );
	var temcoisa := 0;
	foreach item in items
     	   if(item.objtype == 3555)
		itemarray.append(item);
		GFTextLine(gump, (47 + x), (70 + y), 800, "Fogueira/Acampamento");
		GFAddButton(gump, (30 + x), (74 + y),  2103, 2104, GF_CLOSE_BTN, b );
		y := y + 20;
		b := b + 1;
		if (y > 380)
			x := x + 150;
			y := 0;		
		endif
		temcoisa := 1;
     	   elseif(item.isa(POLCLASS_CORPSE))
		itemarray.append(item);
		GFTextLine(gump, (47 + x), (70 + y), 800, "Corpo");
		GFAddButton(gump, (30 + x), (74 + y),  2103, 2104, GF_CLOSE_BTN, b );
		y := y + 20;
		b := b + 1;
		if (y > 380)
			x := x + 150;
			y := 0;		
		endif
		temcoisa := 1;
	   endif
      sleepms(2);
	endforeach

	if (!temcoisa)
		SendSysMessageEx(who, "Não há itens para serem rastreados", 3, 660);
		return;
	endif
	var ifres := GFSendGump(who, gump);	

	if (ifres[0] != 0)
		var mobname := 0;
		var mobontrack := itemarray[ifres[0]];
		CreatureTrack(who, mobontrack,mobname);
	endif
endfunction


//gump individual
function CreatureTrack(who, mobontrack, mobname)
	var local;
	var end := 0;

	while (end != 1)
	var gump := GFCreateGump(50, 25);
//	GFResizePic(gump, 2, 0, BKGND_SILVERTRIM, 300, 220);
	GFResizePic(gump, 0, 25, 0x13BE, 300, 250);
	GFResizePic(gump, 15, 40, 0xBB8, 270, 220);

	//Titulo do gump
	if (mobname == 0)
		GFTextLine(gump, 40, 40, 600, "O que deseja analisar desse item" );
	elseif ((mobname == 1) || (mobname == 2))
		GFTextLine(gump, 40, 40, 600, "O que deseja analisar desse recurso" );
	else
		GFTextLine(gump, 40, 40, 600, "O que deseja analisar do rastro" );
		GFTextLine(gump, 40, 60, 600, "de um(a) " + mobname );
	endif

	GFTextLine(gump, 47, 100, 800, "Localizacao do Alvo");
	GFAddButton(gump, 30, 104,  2103, 2104, GF_CLOSE_BTN, 1 );
	GFTextLine(gump, 47, 120, 800, "Seguir Alvo");
	GFAddButton(gump, 30, 124,  2103, 2104, GF_CLOSE_BTN, 2 );

	if ((mobname != 0) && (mobname != 1))
		GFTextLine(gump, 47, 140, 800, "Profundidade do Rastro");
		GFAddButton(gump, 30, 144,  2103, 2104, GF_CLOSE_BTN, 3 );
		GFTextLine(gump, 47, 160, 800, "Verificar se esta em Bando");
		GFAddButton(gump, 30, 164,  2103, 2104, GF_CLOSE_BTN, 4 );
	endif
	GFTextLine(gump, 47, 180, 800, "Mudar de Alvo");
	GFAddButton(gump, 30, 184,  2103, 2104, GF_CLOSE_BTN, 5 );
	GFTextLine(gump, 47, 200, 800, "Marcar no mapa");
	GFAddButton(gump, 30, 204,  2103, 2104, GF_CLOSE_BTN, 6 );

	var ifres := GFSendGump(who, gump);	


	if (ifres[0] == 1)
		end := DistanceCheck(who, mobontrack);
		if (end != 1)
			var local := MobLocalizate(who, mobontrack);
			SendSysMessageEx(who, "" + local, 3, 191);
		endif
	elseif (ifres[0] == 2)
		end := DistanceCheck(who, mobontrack);
		if (end != 1)
			var parar := 0;
			while (parar < 5)
				var local := MobLocalizate(who, mobontrack);
				SendSysMessageEx(who, "" + local, 3, 191);
				sleepms(5000);
				parar := parar + 1;
			endwhile
		endif
	elseif (ifres[0] == 3)
		end := DistanceCheck(who, mobontrack);
		if (end != 1)
			var dist := MobDistance(who, mobontrack);
			SendSysMessageEx(who, "" + dist, 3, 660);
		endif
	elseif (ifres[0] == 4)
		end := DistanceCheck(who, mobontrack);
		if (end != 1)
			var band := BandVerify(who, mobname, mobontrack);
			SendSysMessageEx(who, "" + band, SSM_INFO);
		endif
	elseif (ifres[0] == 5)
		if (mobname == 0)
			ItemTracking(who);
		else
			MobileTracking(who);
		endif
		end := 1;
	elseif (ifres[0] == 6)
		SendSysMessageEx(who, "Escolha o mapa.", SSM_REQUEST);
		var targmap := 	Target(who);
		if (!targmap.isa(POLCLASS_MAP))
			SendSysMessageEx(who, "Cancelado.", SSM_FAIL);
		else
			if ( (mobontrack.x > targmap.xwest) && (mobontrack.x < targmap.xeast) && (mobontrack.y > targmap.ynorth ) && (mobontrack.y < targmap.ysouth) )
				targmap.appendpin(mobontrack.x, mobontrack.y);
				var pininfo := GetObjProperty(targmap, "pininfo");
				if (pininfo == error)
					pininfo := dictionary;
				endif
				var pin := struct;
				pin.+name := RequestGump(who, "Escreva o nome dessa marca.");
				pininfo[targmap.getpins().size()] := pin;
				SetObjProperty(targmap, "pininfo", pininfo);
				SendSysMessageEx(who, "Você marcou a localizacao no mapa.", SSM_INFO);
			else
				SendSysMessageEx(who, "O alvo não esta na regiao deste mapa.", SSM_INFO);
			endif
		endif
	elseif (ifres[0] == 0)
		end := 1;
		return 0;
	endif
	endwhile

endfunction

function MobDistance(who, mobontrack)
	var dist := Distance(who, mobontrack);
//	sendsysmessageEx(who, "dist " + dist + " " + mobontrack.x + " " + mobontrack.y);
	if (dist > 70) return "perdeu";
		elseif (dist > 60) return "O rastro parece estar quase sumindo";
		elseif (dist > 50) return "O rastro é muito superficial";
		elseif (dist > 40) return "O rastro é superficial";
		elseif (dist > 30) return "O rastro começa a ficar definido";
		elseif (dist > 20) return "O rastro é bem definido";
		elseif (dist > 10) return "O rastro é recente e cheio de detalhes";
		elseif (dist > 1) return "A criatura com esse rastro deve estar bem perto";
	endif

endfunction

function MobDistanceColor(who, mobontrack)
	var dist := Distance(who, mobontrack);
//	sendsysmessageEx(who, "dist " + dist + " " + mobontrack.x + " " + mobontrack.y);
	if (dist > 80) return 833;
		elseif (dist > 70) return 733;
		elseif (dist > 60) return 633;
		elseif (dist > 50) return 533;
		elseif (dist > 40) return 433;
		elseif (dist > 30) return 333;
		elseif (dist > 20) return 233;
		elseif (dist > 10) return 133;
		elseif (dist > 1) return 33;
	endif

endfunction

function MobLocalizate(who, critter)
	var ns_desc:="";
	var ew_desc:="";
	var ret_val:="";

	//sendsysmessageEx(who, " "+ critter.desc + " " + critter.x +  " " + critter.y);

	if ( critter.y < who.y )
		ns_desc := "norte ";
	elseif ( critter.y > who.y )
		ns_desc := "sul ";
	endif
	if ( critter.x < who.x )
		ew_desc := "oeste";
	elseif ( critter.x > who.x )
		ew_desc := "leste";
	endif

	if ( !ns_desc && !ew_desc )
		ret_val := "proximo";
	else
		ret_val := "para o "+ns_desc + " e " +ew_desc;
	endif
	return ret_val;

endfunction

//retorna so a sigla
function MobLocalizateSigla(who, critter)

	var ns_desc:="";
	var ew_desc:="";
	var ret_val:="";

	//sendsysmessageEx(who, " "+ critter.desc + " " + critter.x +  " " + critter.y);

	if ( critter.y < who.y )
		ns_desc := "N";
	elseif ( critter.y > who.y )
		ns_desc := "S";
	endif
	if ( critter.x < who.x )
		ew_desc := "O";
	elseif ( critter.x > who.x )
		ew_desc := "L";
	endif

	if ( !ns_desc && !ew_desc )
		ret_val := "proximo";
	else
		ret_val := ns_desc+ew_desc;
	endif
	return ret_val;

endfunction


function GetMobDesc(who, mobile)
	var itemonmobile := ListEquippedItems( mobile );
   if (CheckLineOfSight( who, mobile ) && (mobile.hidden == 0))
      return mobile.name + " [" + MobLocalizateSigla(who, mobile) + "]";
   endif

   if (CheckGender(mobile))
      foreach item in itemonmobile
         if (item.graphic == 5903) 
            return "Humanoide com Sapatos" + " [" + MobLocalizateSigla(who, mobile) + "]";
         elseif (item.graphic == 5901) 
            return "Humanoide com Sandalhas"+ " [" + MobLocalizateSigla(who, mobile) + "]";
         elseif (item.graphic == 5899)
            return "Humanoide com Botas"+ " [" + MobLocalizateSigla(who, mobile) + "]";
         elseif (item.graphic == 5905) 
            return "Humanoide com Botas Longas"+ " [" + MobLocalizateSigla(who, mobile) + "]";
         elseif (item.graphic == 5137)
            return "Humanoide com Calça de Placas"+ " [" + MobLocalizateSigla(who, mobile) + "]";
         else
            return "Humanoide Descalço"+ " [" + MobLocalizateSigla(who, mobile) + "]";
         endif
         sleepms(2);
      endforeach
   endif

   if (mobile.isa(POLCLASS_NPC))
      var npcdesc := NPC_GetNPCConfig(mobile.template);
      return npcdesc.rastro;
   endif
endfunction

function DistanceCheck(who, mobontrack)
	var dist := MobDistance(who, mobontrack);
	if (dist == "perdeu")
		SendSysMessageEx(who, "Você não consegue mais identificar os rastros desse alvo", 3, 28);
		return 1;
	else
		return 0;
	endif
endfunction

function BandVerify(who, mobname, mobontrack)
	var mobdesc;
	var x := 0;
   	var mobiles := ListMobilesNearLocation(mobontrack.x,mobontrack.y, LIST_IGNORE_Z, 10);

	foreach mobile in mobiles
		if((mobile != who) && (!mobile.dead))
			mobdesc := GetMobDesc(who, mobile);
			if (mobdesc == mobname)
				x := x + 1;
			endif
		endif
      sleepms(2);
	endforeach

	if (x >= 7)
		return "É muito provavel que o alvo esteja em bando.";
	elseif (x >= 3)
		return "Talvez o alvo esteja em bando.";
	else
		return "Você tem quase certeza que não existe um bando.";
	endif
endfunction
