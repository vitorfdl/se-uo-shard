use uo;
use os;
use cfgfile;

include "include/client";
include ":datafile:datafile";
include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":crafting:toolWear";
include ":gathering:mining";

const BLOCK_OF_STONE := 0x5001;
const MINING_ANIMATION := 0x0B;
const MINING_SOUND := 0x126;

DF_DEBUG_MODE := 0;

var resource_data_file := DFOpenDataFile("MiscResources", DF_CREATE);

program SandMining(who, scoop)
	
	SendSysMessage(who, "Onde você deseja minerar pedras?");
	var targ := TargetCoordinates(who);
	
	if ( CoordinateDistance(who.x, who.y, targ.x, targ.y) > 2 )
		SendSysMessage(who, "Muito longe.");
		return;
	elseif ( !CanMine(targ, "Stone") )
		SendSysMessage(who, "Você não pode minerar pedras aqui.");
		return;
	endif
	
	var location_name := CStr( targ.x+"x"+targ.y );
	
	var my_x := who.x;
	var my_y := who.y;
	var my_facing := who.facing;
	
	var i:=RandomDiceRoll("1d5");
	for ( i; i>=0; i:=i-1 )
		if ( !MiningLoop(who, scoop, my_x, my_y, my_facing, location_name) )
			break;
		elseif ( !CheckToolWear(who, scoop, EXTRACAO) )
			break;
		endif
		sleep(3);
	endfor
	SendSysMessage(who, "Você parou de minerar.");
endprogram

function MiningLoop(who, scoop, my_x, my_y, my_facing, location_name)
	
	var i;
	for (i:=0; i < RandomDiceRoll("5d5"); i := i+1)
		PerformAction(who, MINING_ANIMATION);
		PlaySoundEffect(who, MINING_SOUND);
		sleep(1);
		if (!CheckLoop(who, scoop, my_x, my_y, my_facing))
			return 0;
		endif
	endfor
	
	var location_elem := DFFindElement(resource_data_file, location_name, DF_CREATE);
	var resource_type := hex(BLOCK_OF_STONE);
	
	var resource_amount := ResourceAmount( resource_type, location_elem );
		
	if (resource_amount <= 0)
		SendSysMessage(who, "Parece não existir pedra utilizavel por aqui.");
		return 0;
	endif
	
	SetResource(resource_type, location_elem, resource_amount-1);
	
	var difficulty := 15; //CInt(100 - (resource_amount * 100 / MAX_RESOURCE_AMOUNT));
	var dice_roll := rollAttrDice(who, STRENGTH) + getProficiencyBonus(who, EXTRACAO);
	if ( dice_roll > difficulty )
		CreateItemInBackPack(who, BLOCK_OF_STONE, 1);
		SendSysMessage(who, "Você minerou um bloco de pedra.");
		return 1;
	else
		SendSysMessage(who, "Você e incapaz de minerar pedras.");
		return 0;
	endif
endfunction
