use uo;
use os;
use cfgfile;

include "include/client";
include ":datafile:datafile";
include ":attributes:attributeConstants";
include ":attributes:attributes";
include ":crafting:toolWear";
include ":gathering:mining";
include "include/say";

const SCOOP_OF_SAND := 0x6075;
const MINING_ANIMATION := 0x0B;
const MINING_SOUND := 0x126;

DF_DEBUG_MODE := 0;

var resources_data_file := DFOpenDataFile("MiscResources", DF_CREATE);

program SandMining(who, scoop)
	
	SendSysMessageEx(who, "Onde voce quer pegar areia?", SSM_REQUEST);
	var targ := TargetCoordinates(who);
	
	if ( CoordinateDistance(who.x, who.y, targ.x, targ.y) > 2 )
		SendSysMessageEx(who, "Muito longe.", SSM_FAIL);
		return;
	elseif ( !CanMine(targ, "Sand") )
		SendSysMessageEx(who, "Voce nao pode pegar areia aqui.", SSM_FAIL);
		return;
	endif
	
	var location_name := CStr( targ.x+"x"+targ.y );
	
	var my_x := who.x;
	var my_y := who.y;
	var my_facing := who.facing;
	
	while(MiningLoop(who, scoop, my_x, my_y, my_facing, location_name) )
		
		CheckToolWear(who, scoop, EXTRACAO);
		//elseif ( !CheckLoop(who, scoop, my_x, my_y, my_facing))
		//	break;
		if ( AP_GetVital(who, "Stamina") < 5)
			SendSysMessageEx(who, "Voce esta muito cansado para continuar.", SSM_FAIL);
			break;
		endif
		AP_ConsumeVital(who, "Stamina", 5);
		sleep(3);
	endwhile
	SendSysMessageEx(who, "Voce parou de cavar.", SSM_FAIL);
endprogram

function MiningLoop(who, scoop, my_x, my_y, my_facing, location_name)
	
	var i;
	for (i:=0; i < RandomDiceRoll("1d2"); i := i+1)
		PerformAction(who, MINING_ANIMATION);
		PlaySoundEffect(who, MINING_SOUND);
		sleep(1);
		if (!CheckLoop(who, scoop, my_x, my_y, my_facing))
			return 0;
		endif
	endfor
	
	var location_elem := DFFindElement(resources_data_file, location_name, DF_CREATE);
	var resource_type := hex(SCOOP_OF_SAND);
	
	var resource_amount := ResourceAmount( resource_type, location_elem );
		
	if (resource_amount <= 0)
		SendSysMessageEx(who, "Nao tem areia utilizavel por aqui.", SSM_FAIL);
		return 0;
	endif
	
	SetResource(resource_type, location_elem, resource_amount-1);
	
	var difficulty := 15;//CInt(100 - (resource_amount * 100 / MAX_RESOURCE_AMOUNT));
	var dice_roll := rollAttrDice(who, STRENGTH) + getProficiencyBonus(who, EXTRACAO);
	if ( dice_roll > difficulty )
		CreateItemInBackPack(who, SCOOP_OF_SAND, 1);
		SendSysMessageEx(who, "Voce pegou um pouco de areia.", SSM_INFO);
		who.SetAchievement("oremining", 1);
		ExpGain(who, difficulty, dice_roll);
		return 1;
	else
		SendSysMessageEx(who, "Voce e incapaz de pegar areia aqui.", SSM_FAIL);
	endif
endfunction
