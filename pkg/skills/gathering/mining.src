
use uo;
use cfgfile;
include ":gathering:mining";
include ":attributes:attributes";
include ":gumps:gumps";
include ":gumps:gumps_ex";


program mining(who)

	var where := GetMiningRegion(who.x, who.y);
	var cfg := ReadconfigFile(":gathering:config/miningspots");
	var ores := cfg[where].Ores;
	var raridades := SplitWords(cfg[where].Rarity);
	if (ores == error)
		var df := opendatafile(":gathering:miningspots");
		var elem := df.FindElement(where);
		ores := elem.GetProp("Ores");
		raridades := SplitWords(elem.GetProp("Rarity"));
	endif
	var possiveis := {};
	var raridade := {};
	ores := SplitWords(ores);
	var i := 1;
	var k := 1;
	
	var skill := AP_GetSkill(who, WISDOM);
	
	foreach thing in ores 
		var dif := DetermineMiningDifficulty( thing );

		if ( (skill + 10) >=  dif)
			possiveis[i] := thing;
			raridade[i] := raridades[k];
			i := i + 1;
		endif
		k := k + 1;
      sleepms(2);
	endforeach

	var gump := GFCreateGump();
	GFDisposable(gump, 0);
	GFResizePic(gump, 0, 25, 3600, 250, 330);	
	GFResizePic(gump, 15, 40, 0x13BE, 220, 300);
	GFTextMid(gump, 20, 50, 250, 1153, "Minerios Disponiveis");
	
	var y := 80;
	i := 1;
   var itemdesc := ReadConfigFile(":*:itemdesc");
	foreach x in possiveis
		GFTextLine(gump, 35, y, 1153, itemdesc[Hex(x)].desc);
		GFTextLine(gump, 180, y, 1153, GetOreRarityDesc(cint(raridade[i])));
		GFAddButton(gump, 20, y+2, 2117, 2118, 1, 400+i);
		i := i + 1;
		y := y + 20;
      sleepms(2);
	endforeach

	var input := GFSendGump(who, gump);
	input := input[0];
	
	if (input > 400)
		SendsysMessage(who, "Você vai procurar por " + itemdesc[Hex(possiveis[input-400])].desc);
		SetObjProperty(who, "searchore", possiveis[input-400]);
	endif

endprogram
