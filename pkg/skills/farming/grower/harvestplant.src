use uo;
use os;

include "include/client";
include ":attributes:skillCheck";
include ":attributes:include/attributeConstants";
include ":attributes:include/attributes";

Program UseGrower(who, grower)
	if ( !ReserveItem(grower) )
		SendSysMessage(who, "Alguem ja esta colhendo isto.");
		return;
	endif

	var where := struct;
	where.+x := who.x;
	where.+y := who.y;

	var num_loops := RandomDiceRoll("1d3");
	
	var farm_bonus := CInt(GetAttribute(who, "Cooking")/13);
	num_loops := num_loops + farm_bonus;
	
	var sleep_time := 4000 - (farm_bonus * 120);
	
	for ( num_loops; num_loops>=0; num_loops:=num_loops-1 )
		if ( !CanPick(who, grower, where) )
			break;
		elseif ( !HarvestPlant(who, grower) )
			break;
		endif
		
		PerformAction(who, ANIM_BOW);
		sleepms(sleep_time);
	endfor
	
	SendSysMessage(who, "Voce parou de colher a planta...");
endprogram

function HarvestPlant(who, grower)
	var difficulty := CInt(grower.GetDifficulty());
	grower.SetProduceAmount(grower.GetProduceAmount()-1);
	
	SendSysMessage(who, "dificuldade " + difficulty);
	if ( SkillCheck(who, COOKWORK, difficulty) > 0 )
		CreateItemInBackPack(who, grower.GetProduceType(), 1);
		SendSysMessage(who, "Voce guardou os frutos na sua mochila.");
		var seed_chance := grower.GetSeedDice();
		var roll := RandomInt(100)+1;
		if ( roll <= seed_chance )
			SendSysMessage(who, "Voce pegou uma semente.");
			CreateItemInBackPack(who, grower.GetGrowerType(), 1);
		else
			if ( RandomInt(15) == 2 )
				SendSysMessage(who, "Voce pegou um pouco de adubo.");
				CreateItemInBackPack(who, "fertiledirt", 1);
			endif
		endif
		PlaySoundEffect(who, 0x0050);
		return 1;
	else
		SendSysMessage(who, "Voce e incapaz de pegar algo de util daqui.");
		
		return 0;
	endif
endfunction

function CanPick(who, grower, where)
	if ( who.hidden )
		SendSysMessage(who, "Voce parou de colher para ficar escondido.");
		return 0;
	elseif ( grower.IsDead() )
		SendSysMessage(who, "A planta esta morta.");
		return 0;
	elseif ( !grower.CanHarvest() )
		SendSysMessage(who, "Esta planta nao parece estar pronta para ser colhida.");
		return 0;
	elseif ( grower.GetProduceAmount() <= 0 )
		SendSysMessage(who, "Esta planta nao possui nada para ser colhida.");
		return 0;
	elseif ( GetEquipmentByLayer(who, LAYER_MOUNT) )
		SendSysMessage(who, "Voce nao pode colher montado.");
		return 0;
	elseif ( (where.x != who.x) || (where.y != who.y) )
		SendSysMessage(who, "Voce parou de colher.");
		return 0;
	else
		return 1;
	endif
endfunction
