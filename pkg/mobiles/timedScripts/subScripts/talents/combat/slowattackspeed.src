/* $Id: alcohol.src 87 2006-05-23 19:04:43Z sroyalty $
 *
 */

use uo;
use os;
use util;

include "include/client";
include ":timedscripts:timedScripts";
include "include/say";

function StartScript(mobile, attacker := 0, level := 0)
   var weapon := mobile.weapon;
   if (GetObjProperty(mobile, "dswingspeed_targ"))
      weapon := SystemFindObjectBySerial(GetObjProperty(mobile, "dswingspeed_targ"));
   endif

   SetObjProperty(mobile, "dswingspeed_targ", weapon.serial);
   weapon.speed_mod := level * -1;
   while (mobile.connected)
      if (mobile.weapon != weapon)
         weapon.speed_mod := 0;
         weapon := mobile.weapon;
         SetObjProperty(mobile, "dswingspeed_targ", weapon.serial);
         weapon.speed_mod := level * -1;
      endif

      if (!GetObjProperty(mobile, "dswingspeed_targ"))
         SetObjProperty(mobile, "dswingspeed_targ", weapon.serial);
      endif
      sleep(3);
   endwhile
   return 1;
endfunction

function EndScript(mobile, level)
   var weapon := GetObjProperty(mobile, "dswingspeed_targ");
   weapon := SystemFindObjectBySerial(weapon);
   if (weapon)
      weapon.speed_mod += level;
   endif

   EraseObjProperty(mobile, "dswingspeed_targ");
   return 1;
endfunction

program TimedScriptInit(params)
	var mobile := params[1];
	var type   := params[2];
	var level  := params[3];

   if (typeof(type) == "String")
      if (type == "End")
         return EndScript(mobile, level);
      elseif (type == "LogOff")
         return;
      endif
   endif

   return StartScript(mobile, type, level);
endprogram
