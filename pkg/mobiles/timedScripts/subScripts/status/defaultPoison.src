/* $Id: alcohol.src 87 2006-05-23 19:04:43Z sroyalty $
 *
 */

use uo;
use os;
use util;
use math;

include "include/client";
include "include/damage";
include ":timedscripts:timedScripts";
include "include/say";
include ":attributes:vitals";

function StartScript(mobile, attacker := 0, level := 0)
	if ( !level )
		level := 1;
	endif

	if ( attacker )
		SetScriptController(attacker);
	endif

   var dicedmg := "1d2";
   var diff := 12;
   case (level)
      2: dicedmg := "1d6";
         diff := 14;
      3: dicedmg := "2d6";
         diff := 16;
      4: dicedmg := "3d6";
         diff := 18;
      5: dicedmg := "4d6";
         diff := 20;
   endcase

   var success := 0;
   while (!success && !mobile.dead)
      var dmg := RandomDiceRoll(dicedmg);
		damageFLS(mobile, cint(dmg), DMG_POISON, attacker);

      if (rollResistDice(mobile, CONSTITUTION) > diff)
         success := 1;
      endif
      sleep(2);
   endwhile
	
	TS_LowerDuration(mobile, "defaultPoison", -1);
   return 1;
endfunction

function EndScript(mobile)
   return 1;
endfunction

function LogOffScript(mobile)
   return 1;
endfunction

program TimedScriptInit(params)
	var mobile := params[1];
	var type   := params[2];
	var level  := params[3];

   if (typeof(type) == "String")
      if (type == "End")
         return EndScript(mobile);
      elseif (type == "LogOff")
         return LogOffScript(mobile);
      endif
   endif

   return StartScript(mobile, type, level);
endprogram
