use uo;
use os;

include ":attributes:attributes";
include ":timedscripts:timedScripts";
include "include/damage";

program default_poison(params)
	var who := params[1];
	var attacker := params[2];
	var level := CInt(params[3]);

	if ( !level )
		level := 1;
	endif

	if ( attacker )
		SetScriptController(attacker);
	endif

	var stam := 0;
	var poisonedmsg := "";
	var time := 10;
	case (level)
		default:
		1: 
		   stam := 5;
		   poisonedmsg := "Voce se sente um pouco cansado.";
		2: 
		   stam := 10;
		   poisonedmsg := "Voce se sente cansado";
		   time := 8;
		3: 
		   stam := 15;
		   poisonedmsg := "Voce se sente muito cansado.";
		   time := 6;
		4: 
		   stam := 20;
		   poisonedmsg := "Voce esta extremamente cansado!";
		   time := 4;
		5: 
		   stam := 25;
		   poisonedmsg := "Voce esta totalmente sem folego!";
		   time := 2;
	endcase

	while(1)
		PrintTextAbovePrivate(who, poisonedmsg, who);	

		Set_Critical(1);
		var curr := CInt(AP_GetVital(who, "Stamina"));
		if ( curr >= stam )
			AP_SetVital(who, "Stamina", (curr-stam));
		else
			AP_SetVital(who, "Stamina", 0);
		endif
		Set_Critical(0);
		
		sleep(time);
	endwhile

	return;
endprogram

