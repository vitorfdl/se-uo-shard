/*
 * $Id: resurrect.src 1348 2006-05-12 08:47:00Z austin $
 *
 */

use uo;
use file;

include ":death:resurrection";
include ":tn:tngumps";
include ":attributes:attributes";
include ":death:death";
include ":tn:cooldown";
include "include/say";
include ":timedscripts:timedScripts";

program textcmd_ResGump(who)
	var targ;
	if ( who.dead )
		targ := who;
	else
      targ := Target(who);
      if (GetObjproperty(targ, "npctemplate"))
         return ghaiaRessurect(targ);
		elseif ( targ.IsA(POLCLASS_CORPSE) && !GetObjproperty(targ, "npctemplate"))
			targ := SystemFindObjectBySerial(targ.ownerserial);
			if ( !targ )
				SendSysMessage(who, "No owner could be found for that corpse.");
				return 0;
			endif
		endif
		
		if ( !targ.dead )
			SendSysMessage(who, "Target is not dead.");
			return 0;
		endif
	endif

	var motiveress;
	if (who.acctname != targ.acctname && targ.cmdlevel < 1)
		motiveress := cstr(BigRequestGump(who , "Escreva detalhadamente o motivo deste ress:"));
	endif
	if (!motiveress && who.cmdlevel <= 4 && who.acctname != targ.acctname)
		SendSysMessage(who, "Você deve escrever o motivo!");
		return 0;
	endif
	
	if (who.acctname != targ.acctname && who.cmdlevel <= 4)
		LogToFile(":fls_core:ress.log", who.name+" ("+who.acctname+") ressou "+targ.name+" ("+targ.acctname+"): "+motiveress, LOG_DATETIME);
		LogToFile(":fls_core:ress.log", "Detalhes... Morto: {}... DeathPoints: {}... Assassinado: {}".format(_ifElse(GetObjProperty(targ, "morto"), "Sim", "Não"), AP_GetVital(targ, "DP"), _ifElse(GetObjProperty(targ, "WeaponKill"), "Sim", "Não")), LOG_DATETIME);
	endif
	KataWakeUp(targ, targ.GetCorpse());
	EraseObjProperty(targ, "morto");
   CloseGump(who, 0x8777);
endprogram
