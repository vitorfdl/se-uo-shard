
use uo;
use os;
use util;
use math;

include ":attributes:attributes";
include ":timedscripts:timedScripts";
include ":death:death";
include ":mounts:mounts";
include ":death:settings";
include ":attributes:attributes";
include "include/say";
include ":tn:cooldown";
include ":charactercreation:habilidades";

program death(params)

	var corpse := params[1];
	var ghost := params[2];
	
	corpse.decayat := 0;

		//sendsysmessage(ghost, " " + corpse.x + " " + corpse.y);

	if (GetObjProperty(ghost, "forjoumorte") == 1)
		return 1;
	endif
	
	var lastHit := GetObjProperty(ghost, "lastHit");
	var killer := SystemFindObjectBySerial(lastHit.attacker);
	var dmg := lastHit.amount;

	var protecao := Cint(GetObjProperty(ghost, "protecaomorte"));
	var save := 0;
	if (getObjProperty(ghost, "savegame") != error)
		protecao := 1;
		save := 1;
	endif

	//Check no Chain Of Pain, para retirar 1 do copnumber se for o caso.
	if (getObjProperty(corpse, "chainofpain"))
		var seriais := getObjProperty(corpse, "chainofpain");
		var copnumber := getObjProperty(corpse, "copnumber");
		copnumber := copnumber - 1;
		foreach serial in seriais
			var mobile := SystemFindObjectBySerial(serial);
			SetObjProperty(mobile, "copnumber", copnumber);
		endforeach
	endif

	if (corpse.x >= 4855 && corpse.y >= 879)

		if ( corpse.x <= 4869 && corpse.y <=  890)
			corpse.movable := 1;
			var e := MoveObjectToLocation(corpse, 4859, 876, 1, corpse.realm, MOVEOBJECT_FORCELOCATION);
			corpse.movable := 0;
		//	sendsysmessage(ghost, " " + e);
		endif
	endif

	if (corpse.x >= 4919 && corpse.y >= 1044)

		if ( corpse.x <= 4933 && corpse.y <=  1066)
			corpse.movable := 1;
			var e := MoveObjectToLocation(corpse, 4893, 1014, 1, corpse.realm, MOVEOBJECT_FORCELOCATION);
			corpse.movable := 0;
		//	sendsysmessage(ghost, " " + e);
		endif
	endif

	if (getObjProperty(corpse, "oldbodyserial"))
		KataWakeUp(ghost, corpse);
	endif

	if (protecao) // Proteção Contra Morte
		EraseObjProperty(ghost, "protecaomorte");
	elseif (!killer.acctname || ghost.cmdlevel) // Foi morto por NPC ou é GM
		if (!protecao)
			//if (getCoolDown(ghost, "deathtime") == 0 )
			if (killer.npctemplate && killer.script  == ":ghaia:tamed")  //se for tamed nao tira dp
			else
				if (randomint(11) <= 5)
					AP_ConsumeVital(ghost, "DP", 10);
					SendSysMessage(ghost, "Voce sofreu um ferimento grave! Com mais "+floor(AP_GetVital(ghost, "DP") / 10)+" voce vai morrer!");
				endif
			endif
			//	//setcooldown(ghost, "deathtime", 30);
			//endif
			if ( AP_GetVital(ghost, "DP") < 10 || CInt(GetObjProperty(ghost, "morto")) )
				SendSysMessage(ghost, "Voce morreu.");
				SetObjProperty(corpse, "morto", 1);
				SetObjProperty(ghost, "morto", 1);
				SetObjProperty(corpse, "charname", GetObjProperty(ghost, "realname"));
				
				SetObjProperty(corpse, "deathtimer", polcore().systime);
				sleep(5);
				return 1;
			endif
		endif
	endif

	SendSysMessage(ghost, "Voce desmaiou.");

	var pacto := PactoSepulturaCheck(ghost, corpse);
	if (pacto == 1)
		return 234;
	endif

	var tempo := 4; // em minutos
	if ( dmg > CInt(AP_GetVitalMaximumValue(ghost, HITS)/5) )
		tempo := tempo * 2;
	endif
	if ( temhabilidade(ghost, "Instinto de Sobrevivência"))
		tempo := tempo /2;
	endif
	if (save)
		tempo := 0;
	endif	
	
	while (tempo > 0)
		var i;
		var deathprop;
		if (tempo%2 == 0)
			SendSysMessage(ghost, "Voce acordara em "+(tempo/2)+" minuto(s).");
		endif
		if (tempo != 1)
			sleep(10);
		else
			for (i:=3;i>=1;i-=1)
				SendSysMessage(ghost, "Voce acordara em " + i*10 + " segundos...");
				sleep(10);
				if (i <= 0)
					break;
				endif
			endfor
		endif
		
		deathprop := DeathProperties(ghost, corpse, tempo, i);
		if (deathprop == 1)
			break;
		elseif (deathprop == 2)
			return 1;
		endif
		
		for (i:=1;i<=3;i+=1)
			sleep(10);
			if ( GetObjProperty(corpse, "curado") )
				EraseObjProperty(corpse, "prolongardesmaio");
				sleep(2);
				i := "curado";
				break;
			endif
		endfor
		
		deathprop := DeathProperties(ghost, corpse, tempo, i);
		if (deathprop == 1)
			break;
		elseif (deathprop == 2)
			return 1;
		endif
		
		tempo := tempo - 1;
	endwhile
	var i;
	var deathprop := DeathProperties(ghost, corpse, tempo, 0);
	if (deathprop == 2)
		return 1;
	endif
	EraseObjProperty(corpse, "japrolongou");

	KataWakeUp(ghost, corpse);
	
	return 1;
endprogram

/* Move o player
 * Ressucita
 * Equipa os itens no corpo
 * Destroi o corpo
 * Seta os vitals
*/
function RessPlayer(who, corpse)


	MoveObjectToLocation( who, corpse.x, corpse.y, corpse.z, corpse.realm);
	who.frozen := 1;
	sleep(1);
	who.frozen := 0;
	Resurrect(who);

	var mypack := who.backpack;
	if(!mypack)
		return;
	endif

	DestroyItem(GetEquipmentByLayer(who, 0x16)); //layer da robe

	foreach thing in EnumerateItemsInContainer(corpse)
		if(thing.container == corpse)

			MoveObjectToLocation(thing, who.x, who.y, who.z, who.realm);
			MoveItemToContainer(thing, who.backpack);
		endif
	endforeach

	foreach thing in EnumerateItemsInContainer(who.backpack)
			EquipItem(who, thing);
	endforeach
	
	var regen := CInt(DP_GetVitalsRegen());
	if (!regen)
		regen := 0.25;
	endif

	var hp := AP_GetVitalMaximumValue(who, HITS);
	var stam := AP_GetVitalMaximumValue(who, STAMINA);
	AP_SetVital(who, HITS, hp*regen);
	AP_SetVital(who, STAMINA, stam*regen);
	DestroyItem(corpse);

endfunction

function PactoSepulturaCheck(ghost, corpse)

	var pacto := 0;
	if (getobjProperty(ghost, "pactodesepultura"))
		var targserial := getobjProperty(ghost, "pactodesepultura");
		var targ := SystemFindObjectBySerial(targserial);
		var dmg := Cint(getobjProperty(corpse, "pactodesepulturadmg"));
		ApplyDamage(targ, dmg);
		var targhp := AP_GetVital(targ, "hits");
		if (targhp <= 0)
			SendSysMessage(ghost, "Voce voltara a vida com o pacto de sepultura.", 3, 191);
			sleepms(1500);
			KataWakeUp(ghost, corpse);
			pacto := 1;
		endif
		EraseObjProperty(ghost, "pactodesepultura");
		EraseObjProperty(ghost, "pactodesepulturadmg");
		EraseObjProperty(corpse, "pactodesepultura");
		EraseObjProperty(corpse, "pactodesepulturadmg");
	endif
	return pacto;

endfunction

function DeathProperties(ghost, corpse, byref tempo, byref i)
	if ( GetObjProperty(corpse, "curado") )
		SendSysMessage(ghost, "Voce foi curado e acordara em 2 segundos!");
		EraseObjProperty(corpse, "prolongardesmaio");
		sleep(2);
		return 1;
	elseif ( GetObjProperty(corpse, "prolongardesmaio") )
		SendSysMessage(ghost, "Seus batimentos cardiacos se reduzem, e você tem mais dificuldade em recobrar a consciencia.",3 , 0);
		EraseObjProperty(corpse, "prolongardesmaio");
		SetObjProperty(corpse, "japrolongou", 1);
		tempo := tempo * 2;
	elseif ( CInt(GetObjProperty(corpse, "morto")) || CInt(GetObjProperty(ghost, "morto")) )
		SendSysMessage(ghost, "Algo aconteceu... Voce morreu.");
		SetObjProperty(corpse, "morto", 1);
		SetObjProperty(ghost, "morto", 1);
		SetObjProperty(corpse, "deathtimer", polcore().systime);
		SetObjProperty(corpse, "charname", GetObjProperty(ghost, "realname"));
		return 2;
	endif
	
	if (i == "curado" || !ghost.GetCorpse())
		return 1;
	endif
	
endfunction